in_options = 0

instruments = new(16, 1, INT8)

// Initialize
TIMESIGS_CNT = 8
timesigs = new(TIMESIGS_CNT, 1, INT8)
timesigs[0] = 44
timesigs[1] = 34
timesigs[2] = 43
timesigs[3] = 33
timesigs[4] = 64
timesigs[5] = 63
timesigs[6] = 24
timesigs[7] = 84

timesig_btn = new(TIMESIGS_CNT, 1, INT)
tsigidx = 0

tempo_btn = new(4, 1, INT)
tempo_delta = new(4, 1, INT)
tempo_delta[0] = -5
tempo_delta[1] = -1
tempo_delta[2] = 1
tempo_delta[3] = 5
tempo_btn_pos = new(4, 1, INT)
tempo_btn_pos[0] = 0
tempo_btn_pos[1] = 1
tempo_btn_pos[2] = 6
tempo_btn_pos[3] = 7
tick_len = 100

tune_btn = new(19, 1, INT)
tunings = new(16, 1, INT)

wave_btn = new(100, 1, INT)
drum_btn = new(42, 1, INT)

// See drum_list.txt and drum_list.py for code generation
drum_names = new(42, 1, INT)
drum_names[0] = "Bass01"
drum_names[0].pos = 0
drum_names[1] = "Bass02"
drum_names[1].pos = 1
drum_names[2] = "Snare01"
drum_names[2].pos = 29
drum_names[3] = "Snare02"
drum_names[3].pos = 30
drum_names[4] = "Tom01"
drum_names[4].pos = 37
drum_names[5] = "HiClose"
drum_names[5].pos = 13
drum_names[6] = "HiOpen"
drum_names[6].pos = 18
drum_names[7] = "Crash"
drum_names[7].pos = 11
drum_names[8] = "Per01"
drum_names[8].pos = 23
drum_names[9] = "Per02"
drum_names[9].pos = 24
drum_names[10] = "Bass03"
drum_names[10].pos = 2
drum_names[11] = "Tom02"
drum_names[11].pos = 38
drum_names[12] = "Bass04*"
drum_names[12].pos = 3
drum_names[13] = "Bass05*"
drum_names[13].pos = 4
drum_names[14] = "Snare03*"
drum_names[14].pos = 31
drum_names[15] = "Snare04*"
drum_names[15].pos = 32
drum_names[16] = "HiClose02*"
drum_names[16].pos = 14
drum_names[17] = "HiOpen02*"
drum_names[17].pos = 19
drum_names[18] = "HiClose03*"
drum_names[18].pos = 15
drum_names[19] = "HiOpen03*"
drum_names[19].pos = 20
drum_names[20] = "Crash02*"
drum_names[20].pos = 12
drum_names[21] = "RevSym01*"
drum_names[21].pos = 27
drum_names[22] = "Ride01*"
drum_names[22].pos = 28
drum_names[23] = "Tom03*"
drum_names[23].pos = 39
drum_names[24] = "Tom04*"
drum_names[24].pos = 40
drum_names[25] = "OrcDrm01*"
drum_names[25].pos = 22
drum_names[26] = "Bell*"
drum_names[26].pos = 8
drum_names[27] = "Cat*"
drum_names[27].pos = 9
drum_names[28] = "Bass06*"
drum_names[28].pos = 5
drum_names[29] = "Bass07*"
drum_names[29].pos = 6
drum_names[30] = "Snare05*"
drum_names[30].pos = 33
drum_names[31] = "Snare06*"
drum_names[31].pos = 34
drum_names[32] = "Snare07*"
drum_names[32].pos = 35
drum_names[33] = "Tom05*"
drum_names[33].pos = 41
drum_names[34] = "HiOpen04*"
drum_names[34].pos = 21
drum_names[35] = "HiClose04*"
drum_names[35].pos = 16
drum_names[36] = "Clap01*"
drum_names[36].pos = 10
drum_names[37] = "Pesi01*"
drum_names[37].pos = 25
drum_names[38] = "Quick01*"
drum_names[38].pos = 26
drum_names[39] = "Bass08*"
drum_names[39].pos = 7
drum_names[40] = "Snare08*"
drum_names[40].pos = 36
drum_names[41] = "HiClose05*"
drum_names[41].pos = 17

fn reset_instruments() {
    $i = 0
    while ($i < 8) {
        instruments[$i] = $i * 11
        $i + 1
    }
    instruments[8] = 39
    instruments[9] = 32
    instruments[10] = 35
    instruments[11] = 34
    instruments[12] = 20
    instruments[13] = 33
    instruments[14] = 22
    instruments[15] = 40

    clean(tunings, 1000)
}

reset_instruments()

fn options_init() {
    // Time-sig buttons
    $i = 0
    $s = ""
    while ($i < TIMESIGS_CNT) {
        sprintf($s, "%d:%d", timesigs[$i] % 10, timesigs[$i] div 10)
        timesig_btn[$i] = add_btn(0, 0, 0, 20, clone($s), 0, nord6, 0, timesig_callback)
        timesig_btn[$i].idx = $i
        $i + 1
    }

    // Tempo buttons
    $i = 0
    while ($i < 4) {
        if (tempo_delta[$i] < 0) {
            sprintf($s, "%d", tempo_delta[$i])
        } else {
            sprintf($s, "+%d", tempo_delta[$i])
        }
        tempo_btn[$i] = add_btn(0, 0, 0, 20,
            clone($s), nord4, nord6, 0x2040, tempo_callback)
        tempo_btn[$i].val = tempo_delta[$i]
        $i + 1
    }

    // Temperament buttons
    $i = 0
    while ($i < 19) {
        if ($i < 9) {
            sprintf($s, "-%d", 9 - $i)
        } else {
            if ($i == 9) { $s = "0" }
            else { sprintf($s, "+%d", $i - 9) }
        }
        tune_btn[$i] = add_btn(0, 0, 0, 20, clone($s), 0, nord6, 0, tune_callback)
        tune_btn[$i].idx = $i
        $i + 1
    }

    // Instrument buttons
    $i = 0
    while ($i < 42) {
        drum_btn[$i] = add_btn(0, 0, 0, 0, drum_names[$i], 0, nord6, 0, wave_callback)
        drum_btn[$i].idx = $i
        drum_btn[$i].pos = drum_names[$i].pos
        $i + 1
    }

    $i = 0
    while ($i < 100) {
        sprintf($s, "Wave %02d", $i)
        wave_btn[$i] = add_btn(0, 0, 0, 0, clone($s), 0, nord6, 0, wave_callback)
        wave_btn[$i].idx = $i
        $i + 1
    }
    remove($s)
}

fn timesig_callback($sender) {
    tsigidx = $sender.idx
    roll_beat = timesigs[tsigidx] div 10
    roll_bar = timesigs[tsigidx] % 10 * roll_beat
}

fn clamp_tick_len() {
    if (tick_len < 10) { tick_len = 10 }
    if (tick_len > 2000) { tick_len = 2000 }
}

fn tempo_callback($sender) {
    tick_len + $sender.val
    clamp_tick_len()
}

fn tune_callback($sender) {
    tunings[roll_tid] = $sender.idx * 100 + 100
}

fn wave_callback($sender) {
    instruments[roll_tid] = $sender.idx
}

fn divide($x, $w, $n) {
    $xs = new($n + 1, 1, INT16)
    $i = 0
    while ($i <= $n) {
        $xs[$i] = $x + $w / $n * $i
        $i + 1
    }
    ret($xs)
}

fn draw_options() {
    clear(nord0)

    // Place time signature buttons
    $btns_x = roll_x + 80
    $btns_w = roll_w - 86

    print("Beats:Ticks", roll_x + 6, roll_y + 17, nord6, LEFT)
    $xs = divide($btns_x, $btns_w, 8)
    $i = 0
    while ($i < TIMESIGS_CNT) {
        timesig_btn[$i].x = $xs[$i] + 1
        timesig_btn[$i].y = roll_y + 6
        timesig_btn[$i].w = $xs[$i + 1] - $xs[$i] - 2
        timesig_btn[$i].bg = nord4
        timesig_btn[$i].alphas = 0x2040
        $i + 1
    }

    if (tsigidx != -1) {
        timesig_btn[tsigidx].bg = nord9
        timesig_btn[tsigidx].alphas = 0xa088
    }

    // Place tempo widgets
    print("Tick length", roll_x + 6, roll_y + 39, nord6, LEFT)
    $i = 0
    while ($i < 4) {
        tempo_btn[$i].x = $xs[tempo_btn_pos[$i]] + 1
        tempo_btn[$i].y = roll_y + 28
        tempo_btn[$i].w = $xs[tempo_btn_pos[$i] + 1] - tempo_btn[$i].x - 1
        $i + 1
    }

    $bar_x = $xs[2] + 1
    $bar_w = $xs[6] - $xs[2] - 2
    $bar_w0 = round($bar_w * tick_len / 2000)

    transp(0xa0)
    fbox($bar_x, roll_y + 28, $bar_w0, 20, nord9)

    transp(0x20)
    fbox($bar_x + $bar_w0, roll_y + 28, $bar_w - $bar_w0, 20, nord6)

    $s = ""
    sprintf($s, "%d ms", tick_len)
    transp(255)
    print($s, roll_x + 80 + (roll_w - 86) / 2, roll_y + 39, nord6)

    // Track-specific settings
    // Place temperament buttons
    if (roll_tid >= 8) {
        $i = 0
        while ($i < 19) {
            tune_btn[$i].w = 0
            $i + 1
        }
    } else {
        print("Fine tune", roll_x + 6, roll_y + 61, nord6, LEFT)
        $xs = divide($btns_x, $btns_w, 19)
        $i = 0
        while ($i < 19) {
            tune_btn[$i].x = $xs[$i] + 1
            tune_btn[$i].y = roll_y + 50
            tune_btn[$i].w = $xs[$i + 1] - $xs[$i] - 2
            tune_btn[$i].bg = nord4
            tune_btn[$i].alphas = 0x2040
            $i + 1
        }
        $idx = round(tunings[roll_tid] / 100) - 1
        tune_btn[$idx].bg = track_colours[roll_tid]
        tune_btn[$idx].alphas = 0xa088
    }

    // Place instrument buttons
    $btns_x = roll_x + 6
    $btns_y = roll_y + 72
    $btns_w = roll_w - 12
    $btns_h = roll_h - 78

    $i = 0
    while ($i < 42) {
        drum_btn[$i].w = 0
        $i + 1
    }
    $i = 0
    while ($i < 100) {
        wave_btn[$i].w = 0
        $i + 1
    }

    $instruments_cnt = 100
    $columns = 10
    $b = wave_btn
    if (roll_tid >= 8) {
        $instruments_cnt = 42
        $columns = 6
        $b = drum_btn
    }
    $xs = divide($btns_x, $btns_w, $columns)
    $ys = divide($btns_y, $btns_h, 10)

    $i = 0
    while ($i < $instruments_cnt) {
        $idx = $i
        if (roll_tid >= 8) { $idx = drum_btn[$i].pos }
        $r = $idx div $columns
        $c = $idx % $columns
        $b[$i].x = $xs[$c] + 1
        $b[$i].y = $ys[$r] + 1
        $b[$i].w = $xs[$c + 1] - $xs[$c] - 2
        $b[$i].h = $ys[$r + 1] - $ys[$r] - 2
        $b[$i].bg = nord4
        $b[$i].alphas = 0x2040
        $i + 1
    }
    remove($xs)
    remove($ys)

    $c = track_colours[roll_tid]
    $b[instruments[roll_tid]].bg = $c
    $b[instruments[roll_tid]].alphas = 0xa088

    if (roll_tid >= 8) {
        print("* = Org-3 only", roll_x + 6, roll_y + roll_h - 17, nord6, LEFT)
    }
}

opt_tempo_down = 0

fn opt_mouse_move($x, $y) {
    if (opt_tempo_down == 0) { ret }

    $xs = divide(roll_x + 80, roll_w - 86, 8)
    $bar_x = $xs[2] + 1
    $bar_w = $xs[6] - $xs[2] - 2

    tick_len = round(2000 * ($x - $bar_x) / $bar_w)
    clamp_tick_len()
    changed = 1
}

fn opt_mouse_down($x, $y) {
    $xs = divide(roll_x + 80, roll_w - 86, 8)
    $bar_x = $xs[2] + 1
    $bar_w = $xs[6] - $xs[2] - 2

    if ($x >= $bar_x && $x < $bar_x + $bar_w &&
        $y >= roll_y + 28 && $y < roll_y + 48)
    {
        opt_tempo_down = 1
        opt_mouse_move($x, $y)
    }
}

fn opt_mouse_up($x, $y) {
    opt_tempo_down = 0
}
