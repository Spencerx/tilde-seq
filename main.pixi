nord0 = #2e3440
nord1 = #3b4252
nord2 = #434c5e
nord3 = #4c566a
nord4 = #d8dee9
nord5 = #e5e9f0
nord6 = #eceff4
nord7 = #8fbcbb
nord8 = #88c0d0
nord9 = #81a1c1
nord10 = #5e81ac
nord11 = #bf616a
nord12 = #d08770
nord13 = #ebcb8b
nord14 = #a3be8c
nord15 = #b48ead

set_pixel_size(2)

font_img = load("font.png")
set_key_color(font_img, BLACK)
set_font(0x20, font_img, 16, 6)

last_xsize = -1
last_ysize = -1

running = 1
changed = 1
is_down = 0

frames = 0
start_timer(0)

include "buttons.pixi"
include "roll.pixi"

include "organya.pixi"

track_btns = new(16)
track_hotkeys = "QWERTYUI"

fn update_track_btns() {
    $i = 0
    while ($i < 16) {
        if (roll_track_sel & (1 << $i)) {
            track_btns[$i].alphas = 0xf8c0
            track_btns[$i].fg = nord6
        } else {
            track_btns[$i].alphas = 0xd0ff
            track_btns[$i].fg = nord0
        }
        $i + 1
    }
}

fn switch_track_cb($sender) {
    switch_track($sender.track_idx)
    update_track_btns()
    // roll_changed is set to 1 in switch_track()
    // so the screen will be redrawn
}

$i = 0
while ($i < 16) {
    $s = new(1, 1, INT8)
    if ($i < 8) {
        num_to_str($s, $i + 1)
    } else {
        $s[0] = track_hotkeys[$i - 8]
    }
    track_btns[$i] =
        add_btn(0, 0, 19, 19, $s, track_colours[$i], nord6, 0, switch_track_cb)
    track_btns[$i].track_idx = $i
    $i + 1
}

update_track_btns()

menu_btns = new(12)
menu_btns_text = new(12)
menu_btns_bg = new(12)

menu_btns_text[0] = "New"
menu_btns_text[1] = "Open"
menu_btns_text[2] = "Save"
menu_btns_text[3] = "Undo"
menu_btns_text[4] = "Redo"
menu_btns_text[5] = "Cut"
menu_btns_text[6] = "Copy"
menu_btns_text[7] = "Clear"
menu_btns_text[8] = "Paste"
menu_btns_text[9] = "Play"
menu_btns_text[10] = "Options"
menu_btns_text[11] = "Config"

menu_btns_bg[0] = nord10
menu_btns_bg[1] = nord10
menu_btns_bg[2] = nord10
menu_btns_bg[3] = nord9
menu_btns_bg[4] = nord9
menu_btns_bg[5] = nord7
menu_btns_bg[6] = nord7
menu_btns_bg[7] = nord7
menu_btns_bg[8] = nord7
menu_btns_bg[9] = nord8
menu_btns_bg[10] = nord8
menu_btns_bg[11] = nord8

menu_btns_text[11].cb = {
    prefs_dialog()
}

$i = 0
while ($i < 12) {
    $w = get_text_xsize(menu_btns_text[$i]) + 12
    menu_btns[$i] =
        add_btn(0, 0, $w, 19, menu_btns_text[$i],
            menu_btns_bg[$i], nord6, 0xd0ff, menu_btns_text[$i].cb)
    $i + 1
}

fn on_resize($w, $h) {
    $i = 0
    while ($i < 16) {
        track_btns[$i].x = -$w div 2
        track_btns[$i].y = -$h div 2 + ($i + 1) * 19 + ($i >= 8)
        $i + 1
    }

    $i = 0
    $x = -$w div 2 + 19
    while ($i < 12) {
        if ($i == 0 || menu_btns_bg[$i] != menu_btns_bg[$i - 1]) {
            $x + 1
        }
        menu_btns[$i].x = $x
        menu_btns[$i].y = -$h div 2
        $x + menu_btns[$i].w
        $i + 1
    }

    roll_x = -$w div 2 + 19
    roll_y = -$h div 2 + 19
    roll_w = $w - 19
    roll_h = $h - 19
}

fn draw() {
    draw_roll()
    transp(255)
    fbox(-WINDOW_XSIZE div 2, -WINDOW_YSIZE div 2, 19, WINDOW_YSIZE, nord0)
    fbox(-WINDOW_XSIZE div 2, -WINDOW_YSIZE div 2, WINDOW_XSIZE, 19, nord0)
    draw_btns()
}

while (running) {
    scr = get_screen()

    if (last_xsize != WINDOW_XSIZE || last_ysize != WINDOW_YSIZE) {
        resize(scr, WINDOW_XSIZE, WINDOW_YSIZE)
        last_xsize = WINDOW_XSIZE
        last_ysize = WINDOW_YSIZE
        on_resize(WINDOW_XSIZE, WINDOW_YSIZE)
        changed = 1
    }

    if (changed) {
        draw()
        frame()
        changed = 0
    } else {
        sleep(10)
    }

    frames + 1

    while (get_event()) {
        if (EVT[EVT_TYPE] == EVT_QUIT) {
            running = 0
        }
        if (EVT[EVT_TYPE] == EVT_BUTTONDOWN) {
            if (EVT[EVT_FLAGS] & EVT_FLAG_CTRL) { roll_ctrl = 1 }
            if (EVT[EVT_FLAGS] & EVT_FLAG_SHIFT) { roll_shift = 1 }
            $t = -1
            if (EVT[EVT_KEY] >= '1' && EVT[EVT_KEY] <= '8') {
                $t = EVT[EVT_KEY] - '1'
            }
            $i = 0
            while ($i < 8) {
                // +32 for converting upper case to lower case
                if (EVT[EVT_KEY] == track_hotkeys[$i] + 32) {
                    $t = 8 + $i
                    break
                }
                $i + 1
            }
            if ($t != -1) {
                switch_track($t)
                update_track_btns()
            }
        }
        if (EVT[EVT_TYPE] == EVT_BUTTONUP) {
            if ((EVT[EVT_FLAGS] & EVT_FLAG_CTRL) == 0) { roll_ctrl = 0 }
            if ((EVT[EVT_FLAGS] & EVT_FLAG_SHIFT) == 0) { roll_shift = 0 }
        }
        if (EVT[EVT_TYPE] == EVT_MOUSEBUTTONDOWN) {
            if (EVT[EVT_KEY] == KEY_MOUSE_SCROLLUP) {
                roll_mouse_scrollup(EVT[EVT_X], EVT[EVT_Y])
            } else {
                if (EVT[EVT_KEY] == KEY_MOUSE_SCROLLDOWN) {
                    roll_mouse_scrolldown(EVT[EVT_X], EVT[EVT_Y])
                } else {
                    btn_mouse_down(EVT[EVT_X], EVT[EVT_Y])
                    roll_mouse_down(EVT[EVT_X], EVT[EVT_Y])
                    is_down = 1
                }
            }
        }
        if (EVT[EVT_TYPE] == EVT_MOUSEBUTTONUP) {
            btn_mouse_up(EVT[EVT_X], EVT[EVT_Y])
            roll_mouse_up(EVT[EVT_X], EVT[EVT_Y])
            is_down = 0
        }
        if (is_down && EVT[EVT_TYPE] == EVT_MOUSEMOVE) {
            btn_mouse_move(EVT[EVT_X], EVT[EVT_Y])
            roll_mouse_move(EVT[EVT_X], EVT[EVT_Y])
        }
    }

    changed = changed || btn_changed || roll_changed
    btn_changed = 0
    roll_changed = 0
}
