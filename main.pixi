nord0 = #2e3440
nord1 = #3b4252
nord2 = #434c5e
nord3 = #4c566a
nord4 = #d8dee9
nord5 = #e5e9f0
nord6 = #eceff4
nord7 = #8fbcbb
nord8 = #88c0d0
nord9 = #81a1c1
nord10 = #5e81ac
nord11 = #bf616a
nord12 = #d08770
nord13 = #ebcb8b
nord14 = #a3be8c
nord15 = #b48ead

set_pixel_size(2)

font_img = load("font.png")
set_key_color(font_img, BLACK)
set_font(0x20, font_img, 16, 6)

last_xsize = -1
last_ysize = -1

running = 1
changed = 1
is_down = 0

frames = 0
start_timer(0)

include "buttons.pixi"
include "roll.pixi"

add_btn(-120, -90, 50, 30, "OuO", nord4, nord5, 0x203d, {
    printf("AAA\n")
})
special_btn = add_btn(-120, -60, 50, 30, "OvO", nord4, nord5, 0x203d, {
    printf("BBB\n")
})
add_btn(-120, -30, 50, 30, "OwO", nord12, nord5, 0x80a0, {
    printf("CCC\n")
})
add_btn(-120, 0, 50, 30, "OxO", nord13, nord5, 0x80a0, {
    printf("DDD\n")
})

roll_x = -150
roll_y = -100
roll_w = 300
roll_h = 200
add_note(0, 0, 3)
add_note(0, 2, 1)
add_note(1, 4, 16)
add_note(1, 6, 36)

fn draw() {
    transp(255)
    clear(nord0)

    scr = get_screen()
    xsize = get_xsize(scr)
    ysize = get_ysize(scr)

    s = ""
    sprintf(s, "Hello world!\n%d x %d\nFPS: %.2lf", xsize, ysize, frames / get_timer(0) * 1000)
    print(s, 0, 0, nord4)

    sprintf(special_btn.text, "OvO %d", xsize)

    draw_roll()
    draw_btns()
}

while (running) {
    scr = get_screen()

    if (last_xsize != WINDOW_XSIZE || last_ysize != WINDOW_YSIZE) {
        resize(scr, WINDOW_XSIZE, WINDOW_YSIZE)
        last_xsize = WINDOW_XSIZE
        last_ysize = WINDOW_YSIZE
        changed = 1
    }

    if (changed) {
        draw()
        frame()
        changed = 0
    } else {
        sleep(10)
    }

    frames + 1

    while (get_event()) {
        if (EVT[EVT_TYPE] == EVT_QUIT) {
            running = 0
        }
        // +1 and -1 since <control> and <command> on Mac
        // are both recognized as KEY_CTRL
        if (EVT[EVT_TYPE] == EVT_BUTTONDOWN) {
            if (EVT[EVT_KEY] == KEY_CTRL) { roll_ctrl + 1 }
        }
        if (EVT[EVT_TYPE] == EVT_BUTTONUP) {
            if (EVT[EVT_KEY] == KEY_CTRL) { roll_ctrl - 1 }
        }
        if (EVT[EVT_TYPE] == EVT_MOUSEBUTTONDOWN) {
            btn_mouse_down(EVT[EVT_X], EVT[EVT_Y])
            roll_mouse_down(EVT[EVT_X], EVT[EVT_Y])
            is_down = 1
        }
        if (EVT[EVT_TYPE] == EVT_MOUSEBUTTONUP) {
            btn_mouse_up(EVT[EVT_X], EVT[EVT_Y])
            roll_mouse_up(EVT[EVT_X], EVT[EVT_Y])
            is_down = 0
        }
        if (is_down && EVT[EVT_TYPE] == EVT_MOUSEMOVE) {
            btn_mouse_move(EVT[EVT_X], EVT[EVT_Y])
            roll_mouse_move(EVT[EVT_X], EVT[EVT_Y])
        }
    }

    changed = changed || btn_changed || roll_changed
    btn_changed = 0
    roll_changed = 0
}
